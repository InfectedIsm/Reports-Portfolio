# Zaros Part 1 - Findings Report

# Table of contents
- ## [Contest Summary](#contest-summary)
- ## [Results Summary](#results-summary)
- ## High Risk Findings
    - ### [H-01. Logical issue in `LiquidationKeeper::checkUpkeep`: `performLowerBound` lead to skipped liquidatable accounts](#H-01)
    - ### [H-02. New `openInterest` and `skew` values are set to `0` for each user's active market during a successful liquidation](#H-02)
- ## Medium Risk Findings
    - ### [M-01. Inadequate checks to confirm the correct status of the sequecnce/sequencerUptimeFeed](#M-01)
- ## Low Risk Findings
    - ### [L-01. Precision loss favoring user on the amount transfered to protocol in `withdrawMarginUsd`](#L-01)
    - ### [L-02. `removeCollateralFromLiquidationPriority` do not check if `marginCollateralConfiguration.totalDeposited == 0` before, making it possible to have an un-liquidatable margin balance](#L-02)


# <a id='contest-summary'></a>Contest Summary

### Sponsor: Zaros

### Dates: Jul 17th, 2024 - Jul 31st, 2024

[See more contest details here](https://codehawks.cyfrin.io/c/2024-07-zaros)

# <a id='results-summary'></a>Results Summary

### Number of findings:
- High: 2
- Medium: 1
- Low: 2


# High Risk Findings

## <a id='H-01'></a>H-01. Logical issue in `LiquidationKeeper::checkUpkeep`: `performLowerBound` lead to skipped liquidatable accounts            



## Summary

The `performLowerBound` parameter in the `LiquidationKeeper::checkUpkeep` function is used for two distinct purposes: (1) as a [threshold for determining if upkeep is needed](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/external/chainlink/keepers/liquidation/LiquidationKeeper.sol#L52-L54) (2) and as a [starting index](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/external/chainlink/keepers/liquidation/LiquidationKeeper.sol#L67-L74) for selecting accounts to be liquidated.
This will lead to skip liquidation of liquidatable accounts which indexes are below `performLowerBound`

## Vulnerability Details

1. `performLowerBound` is first used as a threshold to determine if upkeep is needed:
   ```Solidity
   File: src/external/chainlink/keepers/liquidation/LiquidationKeeper.sol
   61: ⚠       if (liquidatableAccountsIds.length == 0 || liquidatableAccountsIds.length <= performLowerBound) {
   62:             performData = abi.encode(accountsToBeLiquidated);
   63: 
   64:             return (upkeepNeeded, performData);
   65:         }
   ```

2. It is then used as the starting index for selecting accounts to be liquidated:

   ```Solidity
   File: src/external/chainlink/keepers/liquidation/LiquidationKeeper.sol
   67:         uint256 boundsDelta = performUpperBound - performLowerBound;
   68:         uint256 performLength =
   69:             boundsDelta > liquidatableAccountsIds.length ? liquidatableAccountsIds.length : boundsDelta;
   70: 
   71:         accountsToBeLiquidated = new uint128[](performLength);
   72: 
   73:         for (uint256 i; i < performLength; i++) {
   74: ⚠           uint256 accountIdIndexAtLiquidatableAccounts = performLowerBound + i;
   75:             if (accountIdIndexAtLiquidatableAccounts >= liquidatableAccountsIds.length) {
   76:                 break;
   77:             }
   78: 
   79:             accountsToBeLiquidated[i] = liquidatableAccountsIds[accountIdIndexAtLiquidatableAccounts];
   80:             if (!upkeepNeeded && liquidatableAccountsIds[accountIdIndexAtLiquidatableAccounts] != 0) {
   81:                 upkeepNeeded = true;
   82:             }
   83:         }
   ```

These two purposes are logically distinct and not related.
Using the same parameter for both purposes will lead to unexpected behavior.
Example:

1. `performLowerBound = 3` and `performUpperBound = 10`
2. `checkLiquidatableAccounts` returns an array of `4` accounts **all liquidatables**
3. `boundsDelta = 7` and so bounded to `4` because of `liquidatableAccountsIds.length`
4. the first accessed index inside the for loop is `accountIdIndexAtLiquidatableAccounts = performLowerBound + i = 3`
5. as `liquidatableAccountsIds.length = 4`, only the last element will be accessed `liquidatableAccountsIds[3]`
6. the 3 other accounts are not liquidated

## Impact

Unintended skipping of liquidatable accounts.

## Tools Used

Manual review

## Recommendations

Separate the two concerns into distinct parameters, `performLowerBound` to decide if upkeep is needd, and `startIndex` which could simply be the first element of `liquidatableAccountsIds`
It also seems that `boundsDelta` could simply be `performUpperBound`, as this would restrict the upkeep to this maximum value of iteration.

## <a id='H-02'></a>H-02. New `openInterest` and `skew` values are set to `0` for each user's active market during a successful liquidation            



## Summary

New `openInterest` and `skew` values are set to `0` for each user's active market during a successful liquidation

## Vulnerability Details

When an account is liquidated, all of its positions are closed.\
As closing a position imply new values for the open interest and skew for this market, these values must be updated.\
For this reason, a function `perpMarket::checkOpenInterestLimits` compute the new value for these parameters based on the new state of the position.

The issue is that [the values provided](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/branches/LiquidationBranch.sol#L118-L118) to `perpMarket::updateOpenInterest` [are never set](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/branches/LiquidationBranch.sol#L209), and thus equal to zero.

This is correctly implemented during [standard market order execution](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/branches/SettlementBranch.sol#L456-L460) which calls `perpMarket::checkOpenInterestLimits` and then use the resulting values to feed `perpMarket::updateOpenInterest`.

## Impact

Market Skew and Open Interest parameters are set to 0, which will completely mess up accounting and trade execution.

## Tools Used

Manual review

## Recommendations

Compute the new `openInterest` and `skew` values before calling `perpMarket::updateOpenInterest`.

The `perpMarket::checkOpenInterestLimits` in its current state might not exactly fit the situation, as a liquidation exceeding the market open interest and skew limits would force the call to revert, but liquidating the dangerous position is probably more important than checking those limits.

    
# Medium Risk Findings

## <a id='M-01'></a>M-01. Inadequate checks to confirm the correct status of the sequecnce/sequencerUptimeFeed            



## Summary

The `startedAt` value returned by `sequencerUptimeFeed.latestRoundData()` should be checked against zero as stated in the [Chainlink documentation](https://docs.chain.link/data-feeds/l2-sequencer-feeds#example-code) at the bottom of the code example.

## Vulnerability Details

The [chainlink docs](https://docs.chain.link/data-feeds/l2-sequencer-feeds#example-code) (very end of article, below code example) say that sequencerUptimeFeed can return a 0 value for startedAt if it is called during an "invalid round":

> `startedAt`: This timestamp indicates when the sequencer changed status. **This timestamp returns 0 if a round is invalid**.\
> When the sequencer comes back up after an outage, wait for the `GRACE_PERIOD_TIME` to pass before accepting answers from the data feed.\
> Subtract `startedAt` from `block.timestamp` and revert the request if the result is less than the `GRACE_PERIOD_TIME`.

This was also confirmed by chainlink team:

* <https://github.com/smartcontractkit/documentation/pull/1995>
* <https://discord.com/channels/592041321326182401/605768708266131456/1213847312141525002>
  ![image](https://image.noelshack.com/fichiers/2024/31/3/1722407586-2024-07-31-08h31-17.png)

This makes the implemented check below to be useless if its called in an invalid round (meaning `startedAt == 0`), as `block.timestamp - startedAt` will always be > `Constants.SEQUENCER_GRACE_PERIOD_TIME`:

```solidity
File: src/external/chainlink/ChainlinkUtil.sol
41:         if (address(sequencerUptimeFeed) != address(0)) {
42:             try sequencerUptimeFeed.latestRoundData() returns (
43:                 uint80, int256 answer, uint256 startedAt, uint256, uint80
44:             ) {
45:                 bool isSequencerUp = answer == 0;
46:                 if (!isSequencerUp) {
47:                     revert Errors.OracleSequencerUptimeFeedIsDown(address(sequencerUptimeFeed));
48:                 }
49: 
50:                 uint256 timeSinceUp = block.timestamp - startedAt;
51:❌				if (timeSinceUp <= Constants.SEQUENCER_GRACE_PERIOD_TIME) {
52:                     revert Errors.GracePeriodNotOver();
53:                 }
54:             } catch {
55:                 revert Errors.InvalidSequencerUptimeFeedReturn();
56:             }
57:         }
```

## Impact

inadequate checks to confirm the correct status of the sequencerUptimeFeed in `ChainlinkUtil::getPrice()` contract will cause getPrice() to not revert even when the sequencer uptime feed is not updated or is called in an invalid round.

## Tools Used

Manual review

## Recommendations

Add a check for `startedAt == 0`, this is an example but you might prefer to process this scenario differently:

```diff
--- a/src/external/chainlink/ChainlinkUtil.sol
+++ b/src/external/chainlink/ChainlinkUtil.sol
@@ -51,6 +51,9 @@ library ChainlinkUtil {
         if (timeSinceUp <= Constants.SEQUENCER_GRACE_PERIOD_TIME) {
             revert Errors.GracePeriodNotOver();
         }
+        if(startedAt == 0) {
+            revert Errors.InvalidRound();
+        }
     } catch {
         revert Errors.InvalidSequencerUptimeFeedReturn();
     }
```


# Low Risk Findings

## <a id='L-01'></a>L-01. Precision loss favoring user on the amount transfered to protocol in `withdrawMarginUsd`            



## Summary

When `UD60X18` amounts are converted back to token amounts within `convertUd60x18ToTokenAmount`, value is simply truncated, and do not take into account if the decimal should have been rounded up.

## Vulnerability Details

The `MarginCollateralConfiguration::convertUd60xx18ToTokenAmount` set back a 18 decimal value to a lower decimal representation, which simply [truncate the additional decimals](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/leaves/MarginCollateralConfiguration.sol#L62-L62) from the final representation:

```solidity
File: src/perpetuals/leaves/MarginCollateralConfiguration.sol
57:     function convertUd60x18ToTokenAmount(Data storage self, UD60x18 ud60x18Amount) internal view returns (uint256) {
58:         if (Constants.SYSTEM_DECIMALS == self.decimals) {
59:             return ud60x18Amount.intoUint256();
60:         }
61: 
62:❌   return ud60x18Amount.intoUint256() / (10 ** (Constants.SYSTEM_DECIMALS - self.decimals));
63:     }
```

This means that for example, if `ud60x18Amount = 123456789999999999` and `self.decimals = 8`, the returned value will be `12345678`, ditching the fractional part which is `0.9999999999`.

The issue is that when called in [`TradingAccount::withdrawMarginUsd`](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/leaves/TradingAccount.sol#L451-L468), this is applied on a value that is transfered to the recipient, which is in all occurences where the function is called a protocol address.

Each time the `withdrawMarginUsd` will be called in `TradingAccount::deductAccountMargin`, a protocol unfavorable transfer will occur, which will add up over time.

The higher the volume of the protocol will get, the higher the losses will compound over time.

## Impact

Precision loss against protocol when calculating amount to transfer from user, compounding over time on each transfer.

## Tools Used

Manual review

## Recommendations

Add a parameter to select rounding direction for `convertUd60x18ToTokenAmount`

## <a id='L-02'></a>L-02. `removeCollateralFromLiquidationPriority` do not check if `marginCollateralConfiguration.totalDeposited == 0` before, making it possible to have an un-liquidatable margin balance            



## Summary

It is possible for a user to have an unliquidatable collateral balance, as even though `TradingAccountBranch::depositMargin` check that collateral has configured liquidation priority, if admin decide to call `GlobalConfiguration::removeCollateralFromLiquidationPriority` for this particular collateral, it is possible that in a same block a user tx to deposit is executed before the admin call.\
This balance will then count as margin balance, but will not be accessible to liquidators.

## Vulnerability Details

This can happen by race-condition, which means one tx can simply be placed before the other by bad timing. Especially because on Arbitrum there is no public mempool, and transactions are prioritized [on a first-come first-served basis](https://docs.arbitrum.io/how-arbitrum-works/gas-fees#tips-in-l2).\
This means operator/admin have no options to make sure no one sends collateral right before it get removed from the liquidation priority list.

While the `TradingAccountBranch::depositMargin` function ensure that the collateral is on the liquidation priority list `L342`:

```Solidity
File: src/perpetuals/branches/TradingAccountBranch.sol
317:     function depositMargin(uint128 tradingAccountId, address collateralType, uint256 amount) public virtual {
...:
...:    //* ---- some code ---- *//
...:
340: 
341:         // enforce collateral has configured liquidation priority
342:✅       _requireCollateralLiquidationPriorityDefined(collateralType);
343: 
344:         // get the tokens first
345:         IERC20(collateralType).safeTransferFrom(msg.sender, address(this), amount);
346: 
347:         // then perform the actual deposit
348:         tradingAccount.deposit(collateralType, amountX18);
349: 
350:         emit LogDepositMargin(msg.sender, tradingAccountId, collateralType, amount); 
351:     }
```

While (1) [`GlobalConfiguration::removeCollateralFromLiquidationPriority`](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/leaves/GlobalConfiguration.sol#L119-L166) do not check if `marginCollateralConfiguration.totalDeposited` is zero which is understandable to avoid DoS, (2) [`TradingAccount::getMarginBalanceUsd`](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/leaves/TradingAccount.sol#L169-L201) will count this collateral as user's margin balance.

But we see that `TradingAccount::getMarginBalanceUsd` is used extensively inside the protocol to calculate user's margin balance, to decide if he can [create a market order](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/branches/OrderBranch.sol#L127-L127), if the order [can be filled](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/branches/SettlementBranch.sol#L433-L437), and if the order [must be liquidated](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/branches/LiquidationBranch.sol#L142-L148)

Finally, the last piece that make it risk-free for the user is that `TradingAccount::deductAccountMargin` which is used to seize user's collateral [during liquidation](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/branches/LiquidationBranch.sol#L152-L152) or [when an order is updated](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/branches/SettlementBranch.sol#L495-L495) to seize losses is only able to seize collaterals that are in the `collateralLiquidationPriority` list:

```solidity
File: src/perpetuals/leaves/TradingAccount.sol
488:     function deductAccountMargin(
489:         Data storage self,
490:         FeeRecipients.Data memory feeRecipients,
491:         UD60x18 pnlUsdX18,
492:         UD60x18 settlementFeeUsdX18,
493:         UD60x18 orderFeeUsdX18
494:     )
495:         internal
496:         returns (UD60x18 marginDeductedUsdX18)
497:     {
...:
...:    //* ---- some code ---- *//
...:
503: 
504:         // cache collateral liquidation priority length
505:         uint256 cachedCollateralLiquidationPriorityLength = globalConfiguration.collateralLiquidationPriority.length();
506: 
507:         // loop through configured collateral types
508:         for (uint256 i; i < cachedCollateralLiquidationPriorityLength; i++) {
509:             // get ith collateral type
510:             address collateralType = globalConfiguration.collateralLiquidationPriority.at(i);
511: 
512:             // fetch storage slot for this collateral's config config
513:             MarginCollateralConfiguration.Data storage marginCollateralConfiguration =
514:                 MarginCollateralConfiguration.load(collateralType);
515: 
516:             // get trader's collateral balance for this collateral, scaled to 18 decimals
517:             ctx.marginCollateralBalanceX18 = getMarginCollateralBalance(self, collateralType);
518: 
519:             // skip to next loop iteration if trader hasn't deposited any of this collateral
520:             if (ctx.marginCollateralBalanceX18.isZero()) continue;
521: 
522:             // get this collateral's USD price
523:             ctx.marginCollateralPriceUsdX18 = marginCollateralConfiguration.getPrice();
...:
...:    //* ---- some code ---- *//
...:
555: 
556:             // pnl logic same as settlement & order fee above
557:             if (pnlUsdX18.gt(UD60x18_ZERO) && ctx.pnlDeductedUsdX18.lt(pnlUsdX18)) {
558:                 (ctx.withdrawnMarginUsdX18, ctx.isMissingMargin) = withdrawMarginUsd(
559:                     self,
560:                     collateralType,
561:                     ctx.marginCollateralPriceUsdX18,
562:                     pnlUsdX18.sub(ctx.pnlDeductedUsdX18),
563:                     feeRecipients.marginCollateralRecipient
564:                 );
...:
...:    //* ---- some code ---- *//
...:
578:     }
```

What make it even worse is that if user's position PnL is positive, he will [get its profit credited](https://github.com/Cyfrin/2024-07-zaros/blob/main/src/perpetuals/branches/SettlementBranch.sol#L481-L484).

## Impact

A user can open risk-less positions with an unliquidatable collateral, and as such steal funds.\
When his position will be liquidated, nothing will be seized as user has no seizable collateral.\
If user is in profit, he will be able to get rewarded.

While this require some 'luck' to have the deposit hapenning right before the removal of call, the odds to succeed can be increased as the attacker will most probably know when a collateral will be removed from the list, and make a huge deposit in the same block. The attacker could also simply regularly deposit/withdraw collateral if he has no idea when a collateral will be removed from the list.

## Tools Used

Manual review

## Recommendations

The fix is pretty simple, only collaterals that are in the `collateralLiquidationPriority` should be accounted as user's margin balance.

```diff
diff --git a/src/perpetuals/leaves/TradingAccount.sol b/src/perpetuals/leaves/TradingAccount.sol
index b573fae..f9dcd27 100644
--- a/src/perpetuals/leaves/TradingAccount.sol
+++ b/src/perpetuals/leaves/TradingAccount.sol
@@ -181,6 +181,12 @@ library TradingAccount {
         for (uint256 i; i < cachedMarginCollateralBalanceLength; i++) {
             // read key/value from storage for current iteration
             (address collateralType, uint256 balance) = self.marginCollateralBalanceX18.at(i);
+            GlobalConfiguration.Data storage globalConfiguration = GlobalConfiguration.load();
+            bool isInCollateralLiquidationPriority =
+                globalConfiguration.collateralLiquidationPriority.contains(collateralType);
+            if(!isInCollateralLiquidationPriority)
+                continue;

             // load collateral margin config for this collateral type
             MarginCollateralConfiguration.Data storage marginCollateralConfiguration =
```




____________________________________
# ðŸŸ¡ M-01 - `liquidatorReward` computation use `debtPosition.futureValue` in place of `debtInCollateralToken` in `Liquidate.sol`

## Vulnerability details
 `debtPosition.futureValue` represent the amount of debt expressed in debt token (USDC), while `debtInCollateralToken` is the same amount [expressed in collateral tokens (WETH)](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/Liquidate.sol#L91)

 The function `Liquidate::executeLiquidate()` compute the liquidator reward expressed in collateral tokens, as proven by the `>>`in the code snippet below.

`L91` we see that `debtInCollateralToken` is `debtPosition.futureValue` converted to collateral representation by [`debtTokenAmountToCollateralTokenAmount`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/AccountingLibrary.sol#L26-L35)
Then `L96-98` `liquidatorReward` is equal to the minimum value between both terms the the `min` function. While the first term is expressed in collateral tokens, the second term (`L98`) is expressed in debt tokens.

If the debt token is USDC as in the state of the contest, this means `debtPosition.futureValue` is 6 decimals, meaning it will always be less than `debtInCollateralToken`, hence `liquidatorReward` will always be equal to the second term.

`L100` we see that `liquidatorProfitCollateralToken` has some dimension issues, as a collateral value is sumed with a debt value.

Finally, `L119` we see that this is the value that will be transfered to the liquidator, leading to a wrong accounting.

https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/Liquidate.sol#L75
```solidity
File: src/libraries/actions/Liquidate.sol
075:     function executeLiquidate(State storage state, LiquidateParams calldata params)
076:         external
077:         returns (uint256 liquidatorProfitCollateralToken)
078:     {
...:
...:		//* 	some code 		*//
...:
089: 
090:         uint256 assignedCollateral = state.getDebtPositionAssignedCollateral(debtPosition);
091: >>      uint256 debtInCollateralToken = state.debtTokenAmountToCollateralTokenAmount(debtPosition.futureValue);
092:         uint256 protocolProfitCollateralToken = 0;
093: 
094:         // profitable liquidation
095:         if (assignedCollateral > debtInCollateralToken) {
096: >>          uint256 liquidatorReward = Math.min( //*MIN(
097:                 assignedCollateral - debtInCollateralToken,
098: âŒ>>			 Math.mulDivUp(debtPosition.futureValue, state.feeConfig.liquidationRewardPercent, PERCENT)
099:             );
100: >>          liquidatorProfitCollateralToken = debtInCollateralToken + liquidatorReward;
101:
...:
...:		//* 	some code 		*//
...:
117: 
118:         state.data.borrowAToken.transferFrom(msg.sender, address(this), debtPosition.futureValue);
119: >>       state.data.collateralToken.transferFrom(debtPosition.borrower, msg.sender, liquidatorProfitCollateralToken);
120:         state.data.collateralToken.transferFrom(
121:             debtPosition.borrower, state.feeConfig.feeRecipient, protocolProfitCollateralToken
122:         );
123: 
124:         debtPosition.liquidityIndexAtRepayment = state.data.borrowAToken.liquidityIndex();
125:         state.repayDebt(params.debtPositionId, debtPosition.futureValue);
126:     }
```

## Impact
Wrong computation of liquidator reward.

## Proof of Concept
See explanations in "Vulnerability details" section

## Tools Used
Manual review

## Recommended Mitigation Steps
```diff
diff --git a/src/libraries/actions/Liquidate.sol b/src/libraries/actions/Liquidate.sol
index 59b8de1..e53fd5a 100644
--- a/src/libraries/actions/Liquidate.sol
+++ b/src/libraries/actions/Liquidate.sol
@@ -95,7 +95,7 @@ library Liquidate {
         if (assignedCollateral > debtInCollateralToken) {
             uint256 liquidatorReward = Math.min(
                 assignedCollateral - debtInCollateralToken,
-                Math.mulDivUp(debtPosition.futureValue, state.feeConfig.liquidationRewardPercent, PERCENT)
+                Math.mulDivUp(debtInCollateralToken, state.feeConfig.liquidationRewardPercent, PERCENT)
             );
             liquidatorProfitCollateralToken = debtInCollateralToken + liquidatorReward;
```
____________________________________


____________________________________
# ðŸŸ¡ M-02 -`validateSellCreditMarket` may revert incorrectly when `params.amount` is used as `cashAmountOut/In` value (`exactAmountIn == false`)

## Vulnerability details
Size has [protocol safety parameters](https://docs.size.credit/technical-docs/governance-variables-and-fees) to ensure healthiness of the protocol. One of those parameter is `minimumCreditBorrowAToken`, ensuring that no credit's loan can be smaller than that specific value, considered as 'dust'.

The issue is that the function can incorrectly revert when a user submit an order where the amount represent the cash received by the borrower, as provided `params.amount` that can represent either the cash, or the credit amount, is indifferently compared to `minimumCreditBorrowAToken`.

For example, for a minimum credit is $50, the loan size can vary a lot, depending on the APR. 
If the APR is 50%, then the cash received will be $33.3, and because the comparison is made against `minimumCreditBorrowAToken`, call will revert while it shouldn't.

Please proceed to Proof of Concept to see the full walkthrough.

## Impact
Unexpected revert during market buy/sell credit validation.

## Proof of Concept
[`Size::sellCreditMarket`](https://github.com/code-423n4/2024-06-size/blob/main/src/Size.sol#L188) allow a user to fill a limit-buy credit order waiting in the order book.
The function require as input the [following struct](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/SellCreditMarket.sol#L17-L34) that has been simplified below, keeping only the parameters of interest for the explanation:
```solidity
File: src/libraries/actions/SellCreditMarket.sol
17: struct SellCreditMarketParams {
...:
...:	....	....	....	....	....
...:
23:     // The amount of credit to sell
24:     uint256 amount;
...:
...:	....	....	....	....	....
...:
32:     // Whether amount means credit or cash
33:     bool exactAmountIn;
34: }
```

The user can specify (1) the `amount` of credit he want to sell, or (2) the `amount` of cash he is willing to accept for the credit. 

As there is only one `amount` parameter, the choice is represented by [`bool exactAmountIn`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/SellCreditMarket.sol#L32-L33) parameter that is provided when calling the function. 
If `exactAmountIn == true`, `amount` represent (1), if `false` it is (2).

Before proceding to the order execution, parameters and state are checked to ensure everything works correctly.

This is during that validation step that the issue arise:

https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/SellCreditMarket.sol#L87-L89
```solidity
File: src/libraries/actions/SellCreditMarket.sol
51:     function validateSellCreditMarket(State storage state, SellCreditMarketParams calldata params) external view {
...:
...:
...: 		// ---- ---- ---- some code ---- ---- ---- //
...:
...:
86:         // validate amount
87:         if (params.amount < state.riskConfig.minimumCreditBorrowAToken) {
88:             revert Errors.CREDIT_LOWER_THAN_MINIMUM_CREDIT(params.amount, state.riskConfig.minimumCreditBorrowAToken);
89:         }
...:
...:
...:
```

The issue is that `minimumCreditBorrowAToken` represent the minimum credit that can be sold. 
But `credit = cash + interest` or `cash = credit - interest`, which means `cash <= credit`.
So, it is possible that `cash < minimumCreditBorrowAToken` when `params.amount` represent the cash value, **even if the credit amount is above minimumCreditBorrowAToken**

Practical example:
For example, for a minimum credit is $50, the loan size can vary a lot, depending on the APR. 
If the APR is 50%, then the cash received buy the borrower will be $33.3
This means, depending on `exactAmountIn == true/false`, `params.amount == $33.3` or `params.amount == $50`
While both case correspond to a credit amount equal to `minimumCreditBorrowAToken`, in one case the validation will revert.

## Tools Used
Manual review

## Recommended Mitigation Steps
When `exactAmountIn == false`, compute the corresponding credit amount before comparing it to `minimumCreditBorrowAToken`

```diff
--- a/src/libraries/actions/SellCreditMarket.sol
+++ b/src/libraries/actions/SellCreditMarket.sol

@@ -90,7 +90,10 @@ library SellCreditMarket {
         // validate amount
-        if (params.amount < state.riskConfig.minimumCreditBorrowAToken) {
+        	uint creditAmount = params.exactAmountIn
+                            	? params.amount
+                           	: getCreditAmountIn(params.amount, ...) //simplified for readability
+        if (creditAmount < state.riskConfig.minimumCreditBorrowAToken) {
             revert Errors.CREDIT_LOWER_THAN_MINIMUM_CREDIT(params.amount, state.riskConfig.minimumCreditBorrowAToken);
         }
```


____________________________________
# ðŸŸ¡ M-02 BIS -`validateBuyCreditMarket` may revert incorrectly when `params.amount` is used as `cashAmountIn` value (`exactAmountIn == true`)

## Vulnerability details
Size has [protocol safety parameters](https://docs.size.credit/technical-docs/governance-variables-and-fees) to ensure healthiness of the protocol. One of those parameter is `minimumCreditBorrowAToken`, ensuring that no credit's loan can be smaller than that specific value, considered as 'dust'.

The issue is that the function can incorrectly revert when a user submit an order where the amount represent the cash received by the borrower, as provided `params.amount` that can represent either the cash, or the credit amount, [is indifferently compared to `minimumCreditBorrowAToken`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/BuyCreditMarket.sol#L91-L93).

For example, for a minimum credit is $50, the loan size can vary a lot, depending on the APR. 
If the APR is 50%, then the cash received will be $33.3, and because the comparison is made against `minimumCreditBorrowAToken`, call will revert while it shouldn't.

Please proceed to Proof of Concept to see the full walkthrough.

## Impact
Unexpected revert during market buy validation.

## Proof of Concept
[`Size::buyCreditMarket`](https://github.com/code-423n4/2024-06-size/blob/main/src/Size.sol#L188) allow a user to fill a limit-sell credit order waiting in the order book.
The function require as input the [following struct](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/BuyCreditMarket.sol#L16-L34) that has been simplified below, keeping only the parameters of interest for the explanation:
```solidity
File: src/libraries/actions/BuyCreditMarket.sol
17: struct BuyCreditMarketParams {
...:
...:	....	....	....	....	....
...:
23:     // The amount of credit to buy
24:     uint256 amount;
...:
...:	....	....	....	....	....
...:
32:     // Whether amount means credit or cash
33:     bool exactAmountIn;
34: }
```

The user can specify (1) the `amount` of credit he want to buy, or (2) the `amount` of cash he is willing to give for the credit. 

As there is only one `amount` parameter, the choice is represented by [`bool exactAmountIn`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/BuyCreditMarket.sol#L32-L33) parameter that is provided when calling the function. 
If `exactAmountIn == true`, `amount` represent (2), if `false` it is (1).

Before proceding to the order execution, parameters and state are checked to ensure everything works correctly.

This is during that validation step that the issue arise:

https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/BuyCreditMarket.sol#L91-L93
```solidity
File: src/libraries/actions/BuyCreditMarket.sol
51:     function validateBuyCreditMarket(State storage state, BuyCreditMarketParams calldata params) external view {
...:
...:
...: 		// ---- ---- ---- some code ---- ---- ---- //
...:
...:
90:         // validate amount
91:         if (params.amount < state.riskConfig.minimumCreditBorrowAToken) {
92:             revert Errors.CREDIT_LOWER_THAN_MINIMUM_CREDIT(params.amount, state.riskConfig.minimumCreditBorrowAToken);
93:         }
...:
...:
...:
```

The issue is that `minimumCreditBorrowAToken` represent the minimum credit that can be sold. 
But `credit = cash + interest` or `cash = credit - interest`, which means `cash <= credit`.
So, it is possible that `cash < minimumCreditBorrowAToken` when `params.amount` represent the cash value, **even if the credit amount is above minimumCreditBorrowAToken**

Practical example:
For example, for a minimum credit is $50, the loan size can vary a lot, depending on the APR. 
If the APR is 50%, then the cash received buy the borrower will be $33.3
This means, depending on `exactAmountIn == true/false`, `params.amount == $33.3` or `params.amount == $50`
While both case correspond to a credit amount equal to `minimumCreditBorrowAToken`, in one case the validation will revert.

## Tools Used
Manual review

## Recommended Mitigation Steps
When `exactAmountIn == true`, compute the corresponding credit amount before comparing it to `minimumCreditBorrowAToken`

```diff
--- a/src/libraries/actions/BuyCreditMarket.sol
+++ b/src/libraries/actions/BuyCreditMarket.sol

@@ -88,7 +88,10 @@ library BuyCreditMarket {
         // validate amount
-        if (params.amount < state.riskConfig.minimumCreditBorrowAToken) {
+        	uint creditAmount = params.exactAmountIn
+                            	? getCreditAmountOut(params.amount, ...) //simplified for readability
+                           	: params.amount
+        if (creditAmount < state.riskConfig.minimumCreditBorrowAToken) {
             revert Errors.CREDIT_LOWER_THAN_MINIMUM_CREDIT(params.amount, state.riskConfig.minimumCreditBorrowAToken);
         }
```

____________________________________
# ðŸŸ¡ M-03 - Partial repayment with debt/credit creation (compensation) will not charge fragmentation fee, allowing malicious actors to fragment a credit many times to cause high cost to keeper bots

## Vulnerability details
Because the `repay` function only allow to repay full loan, a `compensate` function has been developped. 

Thanks to Compensate, a user can either :
- Use an existing credit he owns and use it to repay part of its debt, doing that will charge him a `fragmentation fee` as 1 credit is divided in 2 now, [forcing keeper bots or lender to spend more gas in the process](https://docs.size.credit/technical-docs/contracts/2.3-fees#id-2.3.2-fragmentation-fee)
- create a fresh new credit/debt of the fraction he wants to repay. [The steps](https://docs.size.credit/technical-docs/contracts/3.4-repayment#id-3.4.2-partial-repayment-via-compensation) are (1) depositing borrow tokens (2) calling `compensate` with the amount he want to partially repay (3) calling `repay` to repay the debt created during compensate
At the end of the process, the initial debt has been reduced by the `amount` compensated

The issue is that **nothing forces the user to repay at the end**, and no fragmentation fee are applied during this process, so user can fragment the credit an will not be charged of any fragmentation fees.

This happens because `L119` when user compensate by creating a new debt/credit pair to repay, the `creditPositionToCompensate.credit` is set `L123` to `amountToCompensate` (which is correct behavior).

But then `L136` the `exiterCreditRemaining` is calculated as `creditPositionToCompensate.credit - amountToCompensate` which is always zero if entering state `L119`.

Because of that, we see `L146` that fragmentation fee will not be applied (please follow after snippet for more explanation why this is an issue)

https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/Compensate.sol#L106-L106
```solidity
File: src/libraries/actions/Compensate.sol
106:     function executeCompensate(State storage state, CompensateParams calldata params) external {
...:
...: 		// ---- ---- ---- some code ---- ---- ---- //
...:
115:
116:         uint256 amountToCompensate = Math.min(params.amount, creditPositionWithDebtToRepay.credit);
117: 
118:         CreditPosition memory creditPositionToCompensate;
119: >>      if (params.creditPositionToCompensateId == RESERVED_ID) {
120:             creditPositionToCompensate = state.createDebtAndCreditPositions({
121:                 lender: msg.sender,
122:                 borrower: msg.sender,
123: >>              futureValue: amountToCompensate,
124:                 dueDate: debtPositionToRepay.dueDate
125:             });
...:
...: 		// ---- ---- ---- some code ---- ---- ---- //
...:
135:
136:âŒ		 uint256 exiterCreditRemaining = creditPositionToCompensate.credit - amountToCompensate;
137: 
...:
...: 		// ---- ---- ---- some code ---- ---- ---- //
...:
146:>>       if (exiterCreditRemaining > 0) {
147:
148:             uint256 fragmentationFeeInCollateral = Math.min(
149:                 state.debtTokenAmountToCollateralTokenAmount(state.feeConfig.fragmentationFee),
150:                 state.data.collateralToken.balanceOf(msg.sender)
151:             );
152:             state.data.collateralToken.transferFrom(
153:                 msg.sender, state.feeConfig.feeRecipient, fragmentationFeeInCollateral
154:             );
155:         }
156:     }
```

While this might have been done on purpose to allow users to partial repay without paying fragmentation fees, nothing ensure that the user has repaid the newly created credit (which would justifiate why no fragmentation fees, as at the end of the process the number of credit has not changed)

This open-up a possibility for a malicious actor to fractionalize a debt multiple times, [forcing keeper bots or lender to overspend gas in the process](https://docs.size.credit/technical-docs/contracts/2.3-fees#id-2.3.2-fragmentation-fee) on their own funds, rather than paying as expected by the fragmentation fee.

## Impact
A malicious actor (could be a concurrent protocol) can force Size keeper bots to overspend gas by having to claim multiple fractions of debt, causing a direct loss of fund to the protocol, while the fragmentation fee should compensate for this cost.

This also goes against the following invariant `FEES_01: Fragmentation fees are applied whenever there is a credit fractionalization`


## Proof of Concept

Add this test to `test/local/actions/Compensate.t.sol`

```solidity
    function test_audit_compensate_N_times_no_fragmentationFee() public {
        _setPrice(1e18);
        _updateConfig("swapFeeAPR", 0);
        _deposit(alice, usdc, 200e6);	// Alice == LENDER
        _deposit(bob, weth, 400e18);	// Bob == BORROWER
		 // for simpler PoC, APR is 0% as its not necessary to prove the point
        YieldCurve memory curve = YieldCurveHelper.pointCurve(365 days, 0);

		// Alice set a limit-buy order with 0% APR
        _buyCreditLimit(alice, block.timestamp + 365 days, curve);

		// Bob borrow the order up to 100e6 
        uint256 debtPositionId = _sellCreditMarket(bob, alice, RESERVED_ID, 100e6, 365 days, false);
        uint256 creditPositionId = size.getCreditPositionIdsByDebtPositionId(debtPositionId)[0];

		uint256 bobCollateralBalanceBefore = _state().bob.collateralTokenBalance;
        assertEq(size.getUserView(bob).borrowATokenBalance, 100e6);
        assertEq(size.getUserView(bob).debtBalance, 100e6);

		// Bob will compensate the credit up to the minimum allowed which is minimumCreditBorrowAToken
		(uint256 minimumCreditBorrowAToken, ) = size.getCryticVariables();

		// initializing num credit counter
		(, uint256 creditCountBefore) = size.getPositionsCount();

		// Bob is now fractionalizing its credit N times (could be up to credit_size/minimumCreditBorrowAToken)
		uint256 N = 10;
		for(uint i; i < N; i++) {
			vm.prank(bob);
			size.compensate(
				CompensateParams({
					creditPositionWithDebtToRepayId: creditPositionId,
					creditPositionToCompensateId: RESERVED_ID,
					amount: minimumCreditBorrowAToken
				})
			);
		}
		(, uint256 creditCountAfter ) = size.getPositionsCount();

		// we see that there are now N credits
		assertEq(creditCountAfter - creditCountBefore, N);

		uint256 bobCollateralBalanceAfter = _state().bob.collateralTokenBalance;

		// fragmentation transfer fractionner collateral to feeRecipient, but we see balance do not changes
		// which means attacker did not pay for fragmentation
        assertEq(bobCollateralBalanceAfter, bobCollateralBalanceBefore);
        assertEq(size.getUserView(bob).borrowATokenBalance, 100e6);
        assertEq(size.getUserView(bob).debtBalance, 100e6);
    }
```


## Tools Used
Manual review

## Recommended Mitigation Steps
If the `exiterCreditRemaining` formula was done on purpose, then the repay proces could be directly integrated into the compensate process when `params.creditPositionToCompensateId == RESERVED_ID` to ensure it is not used to harm keeper

If the `exiterCreditRemaining` formula is eroneous, then I suggest to replace it with :
```solidity
        uint256 exiterCreditRemaining = creditPositionWithDebtToRepay.credit - amountToCompensate;
```



____________________________________

# _Valids but not accepted per C4 rules_

# ðŸŸ¡âœ… M-05 - An attacker can force a sellCreditMarket/buyCreditMarket call to match with a non-optimal offer by front-running the victim

## Vulnerability details
A malicious lender that has put a limit-buy offer in the orderbook can wait for a borrower to match its offer with a market-sell, and front-run him by swapping its intial offer with one that matches the borrower's `maxAPR`.

The issue with this is that the borrower will not have the optimal offer available on the orderbook, and other lenders that had limit-buy orders with better APR than the malicious lender will not have their order matched while they should have been.

See Proof of Concept for more details

>[!NOTE]
> Identically, the same thing can be done for a market-buy but with `minAPR` where the lender gets the worst APR off the loan.
> Although, for simplicity I will present the issue only from one side

## Impact
- The borrower do not get the best offer available
- The lender with better offer than malicious lender do not get matched as they should

## Proof of Concept
When a lender create a limit-buy order (loanOffer), the [`Size::buyCreditLimit`](https://github.com/code-423n4/2024-06-size/blob/main/src/Size.sol#L166) function is called, which calls the library functions [`validateBuyCreditLimit`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/BuyCreditLimit.sol#L29) and [`executeBuyCreditLimit`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/BuyCreditLimit.sol#L57)

A loanOffer is only made of a due date and the APR curve :
```solidity
File: src/libraries/OfferLibrary.sol
08: struct LoanOffer {
09:     // The maximum due date of the loan offer
10:     // Since the yield curve is defined in relative terms, lenders can protect themselves by
11:     //   setting a maximum timestamp for a loan to be matched
12:     uint256 maxDueDate;
13:     // The yield curve in relative terms
14:     YieldCurve curveRelativeTime;
15: }
```

As we can see, if the lender create a new offer and has an already existing loanOffer, the previous one simply get replaced:
```solidity
File: src/libraries/actions/BuyCreditLimit.sol
57:     function executeBuyCreditLimit(State storage state, BuyCreditLimitParams calldata params) external {
58:         state.data.users[msg.sender].loanOffer =
59:             LoanOffer({maxDueDate: params.maxDueDate, curveRelativeTime: params.curveRelativeTime});
60:         emit Events.BuyCreditLimit(
61:             params.maxDueDate,
62:             params.curveRelativeTime.tenors,
63:             params.curveRelativeTime.aprs,
64:             params.curveRelativeTime.marketRateMultipliers
65:         );
66:     }
```

Let's go now to the borrower side who send a market-sell order through the [`Size::sellCreditMarket`](https://github.com/code-423n4/2024-06-size/blob/main/src/Size.sol#L178-L178) function
Again, that function follow the validate and execute pattern calling the [`validatesellCreditMarket`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/SellCreditMarket.sol#L51-L51) and [`executesellCreditMarket`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/SellCreditMarket.sol#L127) functions.

These 2 functions takes as input parameter a `SellCreditMarketParams` struct:
```solidity
File: src/libraries/actions/SellCreditMarket.sol
17: struct SellCreditMarketParams {
18:     // The lender
19:     address lender;
20:     // The credit position ID to sell
21:     // If RESERVED_ID, a new credit position will be created
22:     uint256 creditPositionId;
23:     // The amount of credit to sell
24:     uint256 amount;
25:     // The tenor of the loan
26:     // If creditPositionId is not RESERVED_ID, this value is ignored and the tenor of the existing loan is used
27:     uint256 tenor;
28:     // The deadline for the transaction
29:     uint256 deadline;
30:     // The maximum APR for the loan
31:     uint256 maxAPR;
32:     // Whether amount means credit or cash
33:     bool exactAmountIn;
34: }
```

Now, we see that there is no way for the borrower matching the lender order to ensure that the order he described into the `SellCreditMarketParams` struct will not change, the only thing that can be controlled is the maximumAPR he is willing to pay off the loan.

Because of this, as explained earlier, this allow a malicious lender to propose cheap loans, wait for a borrower to match with its loan as it will be the best available, and update it with a more expensive loan (higher APR) up to the maxAPR accepted by the other part.
This causes the borrower to not get the best available offer, and other lenders who had better offer to not get matched.

> [!IMPORTANT]
> This can also happen during `liquidateWithReplacement` as [there is the same issue](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/LiquidateWithReplacement.sol#L90-L92)

## Tools Used
Manual review

## Recommended Mitigation Steps
Add a nonce to the `LoanOffer` struct that will be incremented each time a new buy-limit is posted, and allow the borrower to specify the nonce of the lender offer they are going to match, so that it will revert of the nonce has been updated (indicating the order they matched has changed)
Another solution is to require as input in `Size::sellCreditMarket` the expected parameters of the loan to match. If the parameters are different, then it should revert.

Similar mitigations can be applied for `BorrowOffer` and `Size::buyCreditMarket`

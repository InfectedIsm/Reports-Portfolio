# ðŸŸ¡ M01 - Mellow assume 1 stETH == 1 WETH which open-up opportunities for arbitrage

## Vulnerability Detail
https://dune.com/LidoAnalytical/Curve-ETHstETH
https://dune.com/queries/3852516/6479636

Because stETH is the result of staked ETH, depending on market and technical conditions, its demand can vary and its price fluctuate compared to ETH as shown by this [Dune dashboard](https://dune.com/queries/3301517/5529782). 

This means people can buy stETH on the market at discount (`stETH:ETH <1`), and deposit into the vault at the value of `stETH:ETH ==1`, the result is shares minted as if they deposited more stETH that they actually did.

If we look the stETH:ETH exchange rate at a [higher timescale](https://dune.com/LidoAnalytical/Curve-ETHstETH), we can see that there has been times where stETH was trading well bellow 1:1, giving users plenty of time to deposit into the protocol, and wait for price getting more healthy and request a withdraw.

## Impact
User will be able to mint vault shares at discount during depeg events, and withdraw when market get healthier to make profit off other users who deposited during healthy periods.

## Code Snippet

This is the oracle used for wstETH : https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/oracles/WStethRatiosAggregatorV3.sol#L9-L9

```solidity
File: src/oracles/WStethRatiosAggregatorV3.sol
07: contract WStethRatiosAggregatorV3 is IAggregatorV3 {
08:     uint8 public constant decimals = 18;
09:     address public immutable wsteth;
10: 
11:     constructor(address wsteth_) {
12:         wsteth = wsteth_;
13:     }
14: 
15:     function getAnswer() public view returns (int256) {
16:âš 		return int256(IWSteth(wsteth).getStETHByWstETH(10 ** decimals));
17:     }
18: 
19:     function latestRoundData()
20:         public
21:         view
22:         override
23:         returns (uint80, int256, uint256, uint256, uint80)
24:     {
25:         return (0, getAnswer(), block.timestamp, block.timestamp, 0);
26:     }
27: }
```

# ðŸŸ¡ M-02 - `calculateStack` should round-up division when computing `s.ratioX96Value`
_(not submitted but valid finding in final report)_

## Vulnerability Detail
`s.ratioX96Value` is used by `analyzeRequest` to calculate `expectedAmounts` for each tokens which then are transfered to the user on withdrawal process inside `processWithdrawal`

If we look more in details how `s.ratioX96Value` is used, we see that it is used as the denominator of the `coefficientX96`
 computation. Because its value is rounded down and its a denominator, this means the result will be over-estimated :

```solidity
	uint256 coefficientX96 = FullMath.mulDiv(value, Q96, s.ratiosX96Value);
```

Then `coefficientX96`, which is over-estimated is now used as the numerator for the `expectedAmounts` computation:

```solidity
            expectedAmounts[i] = ratiosX96 == 0
                ? 0
                : FullMath.mulDiv(coefficientX96, ratiosX96, Q96);
```

So, the inflated value is transmited to `expectedAmounts`, which is then used as the amount to transfer to the user :

```solidity
File: src/Vault.sol
536:     function processWithdrawals(
537:         address[] memory users![x](image.png)
538:     ) external nonReentrant returns (bool[] memory statuses) {
...:
...:		/* --- some code --- */
...:
547:             (
548:                 bool isProcessingPossible,
549:                 bool isWithdrawalPossible,
550:âš 				 uint256[] memory expectedAmounts
551:			 ) = analyzeRequest(s, request);
...:
...:		/* --- some code --- */
...:
559: 
560:             for (uint256 j = 0; j < s.tokens.length; j++) {
561:                 s.erc20Balances[j] -= expectedAmounts[j];
562:                 IERC20(s.tokens[j]).safeTransfer(
563:                     request.to,
564:âš 			 expectedAmounts[j]
565:                 );
566:             }
```

This will lead to the vault allowing withdrawal above the expected ratio of underlyings, which shouldn't be possible.

## Impact
Vault non compliant with configured ratios

## Code Snippet

```solidity
File: src/Vault.sol
476:     function analyzeRequest(
477:         ProcessWithdrawalsStack memory s,
478:         WithdrawalRequest memory request
479:     ) public pure returns (bool, bool, uint256[] memory expectedAmounts) { //* isProcessingPossible, isWithdrawalPossible, expectedAmounts
480:         uint256 lpAmount = request.lpAmount;
481:         if (
482:             request.tokensHash != s.tokensHash || request.deadline < s.timestamp
483:         ) return (false, false, expectedAmounts);
484: 
485:         uint256 value = FullMath.mulDiv(lpAmount, s.totalValue, s.totalSupply);
486:         value = FullMath.mulDiv(value, D9 - s.feeD9, D9);
487:         uint256 coefficientX96 = FullMath.mulDiv(value, Q96, s.ratiosX96Value);
488: 
489:         uint256 length = s.erc20Balances.length;
490:         expectedAmounts = new uint256[](length);
491:         for (uint256 i = 0; i < length; i++) {
492:             uint256 ratiosX96 = s.ratiosX96[i];
493:             expectedAmounts[i] = ratiosX96 == 0
494:                 ? 0
495:                 : FullMath.mulDiv(coefficientX96, ratiosX96, Q96);
496:             if (expectedAmounts[i] >= request.minAmounts[i]) continue;
497:             return (false, false, expectedAmounts);
498:         }
499:         for (uint256 i = 0; i < length; i++) {
500:             if (s.erc20Balances[i] >= expectedAmounts[i]) continue;
501:             return (true, false, expectedAmounts);
502:         }
503:         return (true, true, expectedAmounts);
504:     }
```

## Tool used
Manual review

## Recommendation
Use `mulDivRoundUp` rather than `mulDiv`

____________________________________

# Valid but not accepted by Sherlock rules

# ðŸŸ¡M-02  Anyone can DoS `vault::removeToken` by sending 1 wei of the token to be removed

## Vulnerability Detail
Vaults can have multiple tokens as underlying assets.
To add a token to a vault, admins must call `vault::addToken(address token)` 
Then user are able to deposit that token into the vault.
If admins decide that a token should be removed from the vault, they must call `vault::removeToken(address token)`
The issue here, is that the function will revert if the amount of tokens held by the vault is non zero.

This can also be DoS'd by a front-run call of `vault::deposit()` 

## Impact
DoS by anyone of the admin-controlled `removeToken()` function

## Code Snippet

Add this test to `tests/mainnet/unit/VaultTest.t.sol`

```solidity
    function test_audit_DoSRemoveToken() external {
        Vault vault = new Vault("Mellow LRT Vault", "mLRT", admin);
        vm.startPrank(admin);
        ERC20TvlModule tvlModule = new ERC20TvlModule();
        vault.addToken(Constants.WETH);
        vault.addTvlModule(address(tvlModule));
		vm.stopPrank();

		//malicious user front-run the 'removeToken' call with a 1 wei transfer
        deal(Constants.WETH, address(this), 1 wei);
		IERC20(Constants.WETH).transfer(address(vault), 1 wei);

		vm.prank(admin);
        vm.expectRevert(abi.encodeWithSignature("NonZeroValue()"));
        vault.removeToken(Constants.WETH);
	}
```

## Tool used
Manual review

## Recommendation

Rather than relying on `balanceOf` to account for deposited tokens, use a state variable. The drawback here is that rebasing tokens wouldn't be supported.
Might be a solution without this drawback (2 step process is an idea) but this would complexify the process.

Regarding the `vault::deposit()` case which would also DoS the `vault::removeToken()` call, adding a pause or a 2-step process would solve the issue.
